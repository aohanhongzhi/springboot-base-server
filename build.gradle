plugins {
    id 'org.springframework.boot' version '2.4.12'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java-library'
}

group = 'hxy.base'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11' // 强制jdk11，不要再使用jdk8了
description = "Spring Boot 基础骨架项目"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    maven { name "Alibaba central"; url "https://maven.aliyun.com/repository/central" }
    maven { name "Alibaba"; url "https://maven.aliyun.com/repository/public" }
    maven { name "Alibaba google"; url "https://maven.aliyun.com/repository/google" }
    maven { name "Alibaba gradle-plugin"; url "https://maven.aliyun.com/repository/gradle-plugin" }
    maven { name "Alibaba spring"; url "https://maven.aliyun.com/repository/spring" }
    maven { name "Alibaba spring-plugin"; url "https://maven.aliyun.com/repository/spring-plugin" }
    maven { name "Alibaba grails-core"; url "https://maven.aliyun.com/repository/grails-core" }
    maven { name "Alibaba apache-snapshots"; url "https://maven.aliyun.com/repository/apache-snapshots" }
    maven {
        url "https://mirrors.huaweicloud.com/repository/maven/"
        name = "华为开源镜像库"
    }
    maven { url "https://repo.spring.io/libs-release" }
    mavenCentral()
    if (version.contains('-')) {
        maven { url "https://repo.spring.io/milestone" }
    }
    if (version.endsWith('-SNAPSHOT')) {
        maven { url "https://repo.spring.io/snapshot" }
    }
}

dependencies {

    // 全局排除tomcat，比如换jetty/undertow就可以用这种方式避免tomcat打包进去
    configurations.all {
        exclude module: 'spring-boot-starter-tomcat'
        resolutionStrategy.cacheChangingModulesFor 0, "minutes"
    }

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation 'org.springframework.boot:spring-boot-starter-undertow'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    implementation 'com.baomidou:mybatis-plus-boot-starter:3.5.1'
    runtimeOnly 'mysql:mysql-connector-java'

    implementation 'com.ejlchina:okhttps-jackson:3.4.0'

    implementation 'org.springframework.retry:spring-retry:1.3.1'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}
